# Accounts-Report
cobollll

IDENTIFICATION DIVISION.
PROGRAM-ID. ACCOUNTS-REPORT.
AUTHOR. BANK-SYSTEM.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT ACCOUNT-FILE ASSIGN TO "ACCOUNTS.DAT"
        ORGANIZATION IS LINE SEQUENTIAL.
    SELECT REPORT-FILE ASSIGN TO "REPORT.TXT"
        ORGANIZATION IS LINE SEQUENTIAL.

DATA DIVISION.
FILE SECTION.
FD ACCOUNT-FILE.
01 ACCOUNT-RECORD.
   05 ACC-NUMBER         PIC X(3).
   05 ACC-NAME           PIC X(25).
   05 ACC-TYPE           PIC X(1).
   05 ACC-BALANCE        PIC 9(6)V99.

FD REPORT-FILE.
01 REPORT-LINE           PIC X(80).

WORKING-STORAGE SECTION.
01 WS-EOF                PIC X VALUE 'N'.
01 WS-PAGE-NUMBER        PIC 9(3) VALUE 1.
01 WS-LINE-COUNT         PIC 99 VALUE 0.
01 WS-RECORD-COUNT       PIC 999 VALUE 0.
01 WS-TOTAL-BALANCE      PIC 9(9)V99 VALUE 0.

01 WS-HEADER1.
   05 FILLER             PIC X(28) VALUE SPACES.
   05 FILLER             PIC X(16) VALUE 'CHINA TRUST BANK'.
   05 FILLER             PIC X(36) VALUE SPACES.

01 WS-HEADER2.
   05 FILLER             PIC X(30) VALUE SPACES.
   05 FILLER             PIC X(13) VALUE 'MAKATI AVENUE'.
   05 FILLER             PIC X(37) VALUE SPACES.

01 WS-HEADER3.
   05 FILLER             PIC X(30) VALUE SPACES.
   05 FILLER             PIC X(11) VALUE 'MAKATI CITY'.
   05 FILLER             PIC X(39) VALUE SPACES.

01 WS-TITLE-LINE.
   05 FILLER             PIC X(24) VALUE SPACES.
   05 FILLER             PIC X(15) VALUE 'ACCOUNTS REPORT'.
   05 FILLER             PIC X(26) VALUE SPACES.
   05 FILLER             PIC X(5) VALUE 'PAGE '.
   05 WS-PAGE-NO         PIC Z9.
   05 FILLER             PIC X(8) VALUE SPACES.

01 WS-COLUMN-HEADER1.
   05 FILLER             PIC X(7) VALUE 'ACCOUNT'.
   05 FILLER             PIC X(17) VALUE SPACES.
   05 FILLER             PIC X(7) VALUE 'ACCOUNT'.
   05 FILLER             PIC X(33) VALUE SPACES.
   05 FILLER             PIC X(7) VALUE 'BALANCE'.
   05 FILLER             PIC X(9) VALUE SPACES.

01 WS-COLUMN-HEADER2.
   05 FILLER             PIC X(6) VALUE 'NUMBER'.
   05 FILLER             PIC X(18) VALUE SPACES.
   05 FILLER             PIC X(4) VALUE 'NAME'.
   05 FILLER             PIC X(52) VALUE SPACES.

01 WS-DETAIL-LINE.
   05 WS-DET-NUMBER      PIC X(3).
   05 FILLER             PIC X(8) VALUE SPACES.
   05 WS-DET-NAME        PIC X(25).
   05 FILLER             PIC X(3) VALUE SPACES.
   05 WS-DET-TYPE        PIC X(1).
   05 FILLER             PIC X(4) VALUE SPACES.
   05 WS-DET-BALANCE     PIC ZZZ,ZZ9.99.
   05 FILLER             PIC X(26) VALUE SPACES.

01 WS-TOTAL-LINE1.
   05 FILLER             PIC X(5) VALUE SPACES.
   05 FILLER             PIC X(30) 
      VALUE 'TOTAL NO. OF RECORDS PRINTED:'.
   05 FILLER             PIC X(20) VALUE SPACES.
   05 WS-TOT-RECORDS     PIC ZZ,ZZ9.
   05 FILLER             PIC X(19) VALUE SPACES.

01 WS-TOTAL-LINE2.
   05 FILLER             PIC X(5) VALUE SPACES.
   05 FILLER             PIC X(27) 
      VALUE 'TOTAL ACCUMULATED BALANCE:'.
   05 FILLER             PIC X(16) VALUE SPACES.
   05 FILLER             PIC X(1) VALUE 'P'.
   05 FILLER             PIC X(4) VALUE SPACES.
   05 WS-TOT-BALANCE     PIC ZZ,ZZZ,ZZ9.99.
   05 FILLER             PIC X(18) VALUE SPACES.

01 WS-BLANK-LINE         PIC X(80) VALUE SPACES.

PROCEDURE DIVISION.
MAIN-PROCEDURE.
    OPEN INPUT ACCOUNT-FILE
    OPEN OUTPUT REPORT-FILE
    
    PERFORM PRINT-HEADERS
    PERFORM READ-ACCOUNT
    PERFORM PROCESS-RECORDS UNTIL WS-EOF = 'Y'
    PERFORM PRINT-TOTALS
    
    CLOSE ACCOUNT-FILE
    CLOSE REPORT-FILE
    STOP RUN.

PRINT-HEADERS.
    WRITE REPORT-LINE FROM WS-BLANK-LINE
    WRITE REPORT-LINE FROM WS-HEADER1
    WRITE REPORT-LINE FROM WS-HEADER2
    WRITE REPORT-LINE FROM WS-HEADER3
    WRITE REPORT-LINE FROM WS-BLANK-LINE
    WRITE REPORT-LINE FROM WS-BLANK-LINE
    MOVE WS-PAGE-NUMBER TO WS-PAGE-NO
    WRITE REPORT-LINE FROM WS-TITLE-LINE
    WRITE REPORT-LINE FROM WS-BLANK-LINE
    WRITE REPORT-LINE FROM WS-COLUMN-HEADER1
    WRITE REPORT-LINE FROM WS-COLUMN-HEADER2
    MOVE 10 TO WS-LINE-COUNT.

READ-ACCOUNT.
    READ ACCOUNT-FILE
        AT END MOVE 'Y' TO WS-EOF
    END-READ.

PROCESS-RECORDS.
    IF WS-LINE-COUNT > 18
        ADD 1 TO WS-PAGE-NUMBER
        WRITE REPORT-LINE FROM WS-BLANK-LINE AFTER PAGE
        PERFORM PRINT-HEADERS
    END-IF
    
    MOVE ACC-NUMBER TO WS-DET-NUMBER
    MOVE ACC-NAME TO WS-DET-NAME
    MOVE ACC-TYPE TO WS-DET-TYPE
    MOVE ACC-BALANCE TO WS-DET-BALANCE
    
    WRITE REPORT-LINE FROM WS-DETAIL-LINE
    ADD 1 TO WS-LINE-COUNT
    ADD 1 TO WS-RECORD-COUNT
    ADD ACC-BALANCE TO WS-TOTAL-BALANCE
    
    PERFORM READ-ACCOUNT.

PRINT-TOTALS.
    WRITE REPORT-LINE FROM WS-BLANK-LINE
    WRITE REPORT-LINE FROM WS-BLANK-LINE
    WRITE REPORT-LINE FROM WS-BLANK-LINE
    MOVE WS-RECORD-COUNT TO WS-TOT-RECORDS
    WRITE REPORT-LINE FROM WS-TOTAL-LINE1
    MOVE WS-TOTAL-BALANCE TO WS-TOT-BALANCE
    WRITE REPORT-LINE FROM WS-TOTAL-LINE2.